buildscript {
    ext.junit_version = '5.1.0'
    ext.kotlin_version = '1.4.31'
}

plugins {
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.jetbrains.dokka' version '1.4.32'
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id 'org.jetbrains.kotlin.kapt' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

group = 'com.jeffpdavidson.kotwords'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

kotlin {
    jvm {
        withJava()

        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }

        compilations.test.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    js {
        browser {
            dceTask {
                keep 'kotwords'
            }
        }
    }
}

dependencies {
    commonMainImplementation "io.ktor:ktor-io:1.5.4"
    commonMainImplementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.1.0"
    commonMainImplementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"
    commonMainImplementation "net.mamoe.yamlkt:yamlkt:0.9.0"
    commonMainImplementation "io.github.pdvrieze.xmlutil:serialization:0.81.2"

    // TODO: Migrate to kotlinx-datetime if parsing/formatting support is added.
    commonMainImplementation "com.soywiz.korlibs.klock:klock:2.0.7"

    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common"

    jvmMainImplementation "org.apache.pdfbox:pdfbox:2.0.9"
    jvmMainImplementation "org.glassfish:javax.json:1.1.2"
    jvmMainImplementation "org.jsoup:jsoup:1.11.2"

    jvmTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    jvmTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"

    jsMainImplementation(npm("jszip", "3.4.0"))
    jsMainImplementation(npm("text-encoding", "0.7.0"))
    jsMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.4.2"
    jsMainImplementation "org.jetbrains.kotlinx:kotlinx-html-js:0.7.3"

    jsTestImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    archiveClassifier.set("javadoc")
    from(dokkaHtml.outputDirectory)
}

publishing {
    publications.withType(MavenPublication) {
        artifact(dokkaJar)
        pom {
            name = 'Kotwords'
            description = 'Collection of crossword puzzle file format converters and other utilities, written in Kotlin.'
            url = 'https://jpd236.github.io/kotwords'
            developers {
                developer {
                    id = 'jpd236'
                    name = 'Jeff Davidson'
                }
            }
            licenses {
                license {
                    name = "The Apache License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
            scm {
                connection = 'scm:git:git://github.com/jpd236/kotwords.git'
                developerConnection = 'scm:git:ssh://git@github.com/jpd236/kotwords.git'
                url = 'https://github.com/jpd236/kotwords'
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.env.OSSRH_DEPLOY_USERNAME
            password = System.env.OSSRH_DEPLOY_PASSWORD
        }
    }
}
